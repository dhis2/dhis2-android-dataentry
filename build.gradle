apply plugin: "com.gradle.build-scan"

buildscript {
    repositories {
        jcenter()

        // required by retrolambda
        mavenCentral()

        // Required by build-scan and error-prone plugins.
        maven {
            url("https://plugins.gradle.org/m2/")
        }

        // required by crashlytics
        maven {
            url("https://maven.fabric.io/public")
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.0'

        // provides insight on how many method references
        // are present in dex file
        classpath "com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.6.1"

        // semantic, static analysis of the source code
        classpath "net.ltgt.gradle:gradle-errorprone-plugin:0.0.8"

        // allows to set BuildConfig fields during compilation
        // without breaking incremental compilation
        classpath "hu.supercluster:paperwork-plugin:1.2.7"

        // enables cloud features of gradle plugin
        classpath "com.gradle:build-scan-plugin:1.3"

        // https://docs.fabric.io/android/changelog.html#fabric-gradle-plugin
        classpath "io.fabric.tools:gradle:1.22.0"

        // provides missing implementation of lambdas from java 8
        classpath "me.tatarka:gradle-retrolambda:3.5.0"

        // generates test reports with screenshots
        classpath "com.stanfy.spoon:spoon-gradle-plugin:1.2.2"

        // static analysis tool
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.2.1"
    }
}

buildScan {
    licenseAgreementUrl = "https://gradle.com/terms-of-service"
    licenseAgree = "yes"
}

allprojects {
    repositories {
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext {
    versions = [
            sdk   : 25,
            tools : "25.0.2",
            minSdk: 15
    ]

    libraries = [
            support          : "25.2.0",
            crashlytics      : "2.6.5",
            paperwork        : "1.2.7",
            dagger           : "2.8",
            timber           : "4.5.0",
            leakcanary       : "1.5",
            dhis2sdk         : "0.1.0-SNAPSHOT",
            rxjava           : "2.0.4",
            rxandroid        : "2.0.1",
            rxbindings       : "2.0.0",
            rxjavainteroperop: "0.9.0",
            progressbar      : "1.2.0",
            butterknife      : "8.5.1",
            okhttp           : "3.5.0",
            sqlbrite         : "1.1.1",
            autovalue        : "1.3",
            autovaluecursor  : "1.0.1",

            // test dependencies
            equalsverifier   : "2.2.1",
            espresso         : "2.2.2",
            mockito          : "2.6.2",
            powermock        : "1.7.0RC2",
            assertj          : "2.5.0",
            junit            : "4.12",
            spoon            : "1.7.0",

            // plugins
            errorpronecore   : "2.0.15"
    ]
}
